//##################################### quantizers.lib ########################################
// Faust Frequency Quantization Library. Its official prefix is `qu`.
//########################################################################################

ba = library("basics.lib"); //useful for the ba.tabulate function. Not used here
it = library("interpolators.lib");

declare name "Faust Frequency Quantization Library";
declare version "0.1";

//=============================Functions Reference========================================
//========================================================================================

/************************************************************************
************************************************************************
This is free and unencumbered software released into the public domain.

Anyone is free to copy, modify, publish, use, compile, sell, or
distribute this software, either in source code form or as a compiled
binary, for any purpose, commercial or non-commercial, and by any
means.

In jurisdictions that recognize copyright laws, the author or authors
of this software dedicate any and all copyright interest in the
software to the public domain. We make this dedication for the benefit
of the public at large and to the detriment of our heirs and
successors. We intend this dedication to be an overt act of
relinquishment in perpetuity of all present and future rights to this
software under copyright law.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
IN NO EVENT SHALL THE AUTHORS BE LIABLE FOR ANY CLAIM, DAMAGES OR
OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE,
ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR
OTHER DEALINGS IN THE SOFTWARE.

For more information, please refer to <http://unlicense.org/>
************************************************************************
************************************************************************/

MinFreq = 8;
MaxFreq = 20000;
TableSize = 256;

qLog(x) = tabulateLocal(1,log(_),TableSize,MinFreq,MaxFreq,x).cub;
qSmooth(x) = tabulateLocal(1,0.5*atan(_*20-10)/1.4711+0.5,TableSize,0,1,x).cub;

tabulateLocal(C, fun, size, r0, r1, x) = environment {

    // Maximum index to access
    mid = size-1;

    // Create the table
    wf(size) = r0 + float(ba.time)*(r1-r0)/float(mid) : fun;

    // Prepare the 'float' table read index
    id = (x-r0)/(r1-r0)*mid;

    // Limit the table read index in [0, mid] if C = 1
    rid(x, 0) = x;
    rid(x, 1) = max(0, min(x, mid));

    // Tabulate an unary 'fun' function on a range [r0, r1]
    val = y0 with { y0 = rdtable(size, wf(size), rid(int(id), C)); };

    // Tabulate an unary 'fun' function on a range [r0, r1] with linear interpolation
    lin = it.interpolate_linear(d,y0,y1)
    with {
        x0 = int(id);
        x1 = x0+1;
        d  = id-x0;
        y0 = rdtable(size, wf(size), rid(x0, C));
        y1 = rdtable(size, wf(size), rid(x1, C));
    };

    // Tabulate an unary 'fun' function on a range [r0, r1] with cubic interpolation
    cub = it.interpolate_cubic(d,y0,y1,y2,y3)
    with {
        x0 = x1-1;
        x1 = int(id);
        x2 = x1+1;
        x3 = x2+1;
        d  = id-x1;
        y0 = rdtable(size, wf(size), rid(x0, C));
        y1 = rdtable(size, wf(size), rid(x1, C));
        y2 = rdtable(size, wf(size), rid(x2, C));
        y3 = rdtable(size, wf(size), rid(x3, C));
    };
};


//-------`(qu.)quantize`----------
// Configurable frequency quantization tool. Outputs only the frenquencies that are part of the specified scale.
//
// Works for positive audio frequencies.
//
// #### Usage
//
// ```
// _ : quantize(rf,nl) : _
// ```
// Where :
//
// * `rf` : frequency of the root note of the scale.
// * `nl` : list of the ratio of the frequencies of each note in relation to the root frequency.
//------------------------
quantize(rf,nl) = _<: octave,_ <: _,!,noteRatio(nN) : rf*_*_
with{
    octave = (qLog(_)-log(rf))/log(2)<:
        (_<0)*(1/(1<<(-1*(_-1)):int))+
        (_>=0)*(1<<(_:int));
    nN = ba.count(nl);
    noteRatio(1,oct) = 1;
    noteRatio(n,oct) = _ <: (ba.take(n,nl)<=((_/oct)/rf))*ba.take(n,nl)+
                            (ba.take(n,nl)>((_/oct)/rf))*(noteRatio(n-1,oct));
};


//-------`(qu.)quantizeSmoothed`----------
// Configurable frequency quantization tool. Outputs frenquencies that are closer to the frequencies of the notes of the specified scale.
//
// Works for positive audio frequencies.
//
//
// #### Usage
//
// ```
// _ : quantizeSmoothed(rf,nl) : _
// nl = (1,1.2,1.4,1.7);
// ```
// Where :
//
// * `rf` : frequency of the root note of the scale.
// * `nl` : list of the ratio of the frequencies of each note in relation to the root frequency.
//------------------------
quantizeSmoothed(rf,nl) = _<: octave,_ <: _,!,noteRatio(nN) : rf*_*_
with{
    octave = (qLog(_)-log(rf))/log(2)<:
        (_<0)*(1/(1<<(-1*(_-1)):int))+
        (_>0)*(1<<(_:int))+
		(_==0)*2;
    nN = ba.count(nl);
    noteRatio(1,oct) = findValue(1,oct);
    noteRatio(n,oct) = _ <: (ba.take(n,nl)<=((_/oct)/rf))*findValue(n,oct)+
                            (ba.take(n,nl)>((_/oct)/rf))*(noteRatio(n-1,oct));

    findValue(n,oct) =  _<: (n<nN)*
            (((_/oct)/rf)-ba.take(n,nl))/((ba.take(min(n+1,nN)-(n==nN),nl)-ba.take(n,nl)))
                    +(n==nN)*
            (((_/oct)/rf)-ba.take(n,nl))/(2-ba.take(n,nl)):       
            qSmooth :  //smoothing curve
            _*(
                (n<nN)*(ba.take(min(n+1,nN)-(n==nN),nl)-ba.take(n,nl))+
                (n==nN)*(2-ba.take(n,nl))
            )+ba.take(n,nl);
};


//---------------------`(qu.)ionian`--------------------------
// List of the frequency ratios of the notes of the ionian mode.
//
// #### Usage
// ```
// _ : quantize(rf,ionian) : _
// ```
//
// Where:
//
// * `rf`: frequency of the root note of the scale.
//-------------------------------------------------------------
ionian = (1,pow(2,2/12),pow(2,4/12),pow(2,5/12),pow(2,7/12),pow(2,9/12),pow(2,11/12));

//---------------------`(qu.)dorian`--------------------------
// List of the frequency ratios of the notes of the dorian mode.
//
// #### Usage
// ```
// _ : quantize(rf,dorian) : _
// ```
//
// Where:
//
// * `rf`: frequency of the root note of the scale.
//-------------------------------------------------------------
dorian = (1,pow(2,2/12),pow(2,3/12),pow(2,5/12),pow(2,7/12),pow(2,8/12),pow(2,10/12));

//---------------------`(qu.)phrygian`--------------------------
// List of the frequency ratios of the notes of the phrygian mode.
//
// #### Usage
// ```
// _ : quantize(rf,phrygian) : _
// ```
//
// Where:
//
// * `rf`: frequency of the root note of the scale.
//-------------------------------------------------------------
phrygian = (1,pow(2,1/12),pow(2,3/12),pow(2,5/12),pow(2,7/12),pow(2,8/12),pow(2,10/12));

//---------------------`(qu.)lydian`--------------------------
// List of the frequency ratios of the notes of the lydian mode.
//
// #### Usage
// ```
// _ : quantize(rf,lydian) : _
// ```
//
// Where:
//
// * `rf`: frequency of the root note of the scale.
//-------------------------------------------------------------
lydian = (1,pow(2,2/12),pow(2,4/12),pow(2,6/12),pow(2,7/12),pow(2,9/12),pow(2,11/12));

//---------------------`(qu.)mixo`--------------------------
// List of the frequency ratios of the notes of the mixolydian mode.
//
// #### Usage
// ```
// _ : quantize(rf,mixo) : _
// ```
//
// Where:
//
// * `rf`: frequency of the root note of the scale.
//-------------------------------------------------------------
mixo = (1,pow(2,2/12),pow(2,4/12),pow(2,5/12),pow(2,7/12),pow(2,9/12),pow(2,10/12));

//---------------------`(qu.)eolian`--------------------------
// List of the frequency ratios of the notes of the eolian mode.
//
// #### Usage
// ```
// _ : quantize(rf,eolian) : _
// ```
//
// Where:
//
// * `rf`: frequency of the root note of the scale.
//-------------------------------------------------------------
eolian = (1,pow(2,2/12),pow(2,3/12),pow(2,5/12),pow(2,7/12),pow(2,8/12),pow(2,10/12));

//---------------------`(qu.)locrian`--------------------------
// List of the frequency ratios of the notes of the locrian mode.
//
// #### Usage
// ```
// _ : quantize(rf,locrian) : _
// ```
//
// Where:
//
// * `rf`: frequency of the root note of the scale.
//-------------------------------------------------------------
locrian = (1,pow(2,1/12),pow(2,3/12),pow(2,5/12),pow(2,6/12),pow(2,8/12),pow(2,10/12));


//---------------------`(qu.)pentanat`--------------------------
// List of the frequency ratios of the notes of the pythagorean tuning for the minor pentatonic scale.
//
// #### Usage
// ```
// _ : quantize(rf,pentanat) : _
// ```
//
// Where:
//
// * `rf`: frequency of the root note of the scale.
//-------------------------------------------------------------
pentanat = (1,32/27,4/3,3/2,16/9);


//---------------------`(qu.)kumoi`--------------------------
// List of the frequency ratios of the notes of the kumoijoshi, the japanese pentatonic scale.
//
// #### Usage
// ```
// _ : quantize(rf,kumoi) : _
// ```
//
// Where:
//
// * `rf`: frequency of the root note of the scale.
//-------------------------------------------------------------
kumoi = (1,16/15,4/3,3/2,8/5);


//---------------------`(qu.)natural`--------------------------
// List of the frequency ratios of the notes of the natural major scale.
//
// #### Usage
// ```
// _ : quantize(rf,natural) : _
// ```
//
// Where:
//
// * `rf`: frequency of the root note of the scale.
//-------------------------------------------------------------
natural = (1,9/8,5/4,4/3,3/2,5/3,15/8);


//---------------------`(qu.)dodeca`--------------------------
// List of the frequency ratios of the notes of the dodecaphonic scale.
//
// #### Usage
// ```
// _ : quantize(rf,dodeca) : _
// ```
//
// Where:
//
// * `rf`: frequency of the root note of the scale.
//-------------------------------------------------------------
dodeca = (1,pow(2,1/12),pow(2,2/12),pow(2,3/12),pow(2,4/12),pow(2,5/12),pow(2,6/12),pow(2,7/12),pow(2,8/12),pow(2,9/12),pow(2,10/12),pow(2,11/12));

//---------------------`(qu.)dimin`--------------------------
// List of the frequency ratios of the notes of the diminished scale.
//
// #### Usage
// ```
// _ : quantize(rf,dimin) : _
// ```
//
// Where:
//
// * `rf`: frequency of the root note of the scale.
//-------------------------------------------------------------
dimin = (1,pow(2,1/12),pow(2,3/12),pow(2,4/12),pow(2,6/12),pow(2,7/12),pow(2,9/12),pow(2,10/12));

//---------------------`(qu.)penta`--------------------------
// List of the frequency ratios of the notes of the minor pentatonic scale.
//
// #### Usage
// ```
// _ : quantize(rf,penta) : _
// ```
//
// Where:
//
// * `rf`: frequency of the root note of the scale.
//-------------------------------------------------------------
penta = (1,pow(2,3/12),pow(2,5/12),pow(2,7/12),pow(2,10/12));
